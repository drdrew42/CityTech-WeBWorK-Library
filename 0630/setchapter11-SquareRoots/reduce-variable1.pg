##DESCRIPTION
##  Algebra: exponents and radicals 
##ENDDESCRIPTION


## DBsubject(Algebra)
## DBchapter(Properties of exponents, rational exponents and radicals)
## DBsection(Properties of rational exponents and radicals)
## Date('2/15/2016')
## Author('Parker')
## Institution('City Tech')
## KEYWORDS('algebra', 'radicals', 'exponents')

########################################################################

DOCUMENT();      

loadMacros(
   "PGstandard.pl",     # Standard macros for PG language
   "MathObjects.pl",
   "contextLimitedPowers.pl"
);

########################################################################

$showPartialCorrectAnswers = 0;

TEXT(beginproblem());

   # code essentially from Davide Cervone 4/25/10
    ###########################
    #
    # Subclass the numeric functions
    #
    package my::Function::numeric;
    our @ISA = ('Parser::Function::numeric');

    #
    # Override sqrt() to return a special value (usually 1) when evaluated
    # effectively eliminating it from the product.
    #
    sub sqrt {
      my $self = shift;
      my $value = $self->context->flag("setSqrt");
 
 return $value+2 if $value && $_[0] == 1; # force sqrt(1) to be incorrect
      return $value if $value;
      return $self->SUPER::sqrt(@_);
    }

    #
    # end of subclass
    #
package main;

    Context("Numeric")->variables->are(
      y => ["Real", limits => [0,2]],    # only needed if x is used in the square roots
    );
    #
    # make sqrt() use our subclass
    #
    Context()->functions->set(sqrt=>{class=>'my::Function::numeric'});
    Context()->flags->set(reduceConstantFunctions=>0);
    #
   #
    # Don't allow fractional powers (avoids 1/2 power)
    # [Could subclass exponentiation to handle that as well]
    #
    LimitedPowers::OnlyPositiveIntegers();

$reducedSqrt = sub  {
      my ($correct,$student,$ans) = @_;
      return 0 if $ans->{isPreview} || $correct != $student;
      #
      #  Get parsed formula for student and correct answers
      #
      $student = $ans->{student_formula};
      $correct = $correct->{original_formula} if defined $correct->{original_formula};
      #
      # check if equal when sqrt's are replaced by 1
      #
      Context()->flags->set(setSqrt => 1);
      delete $correct->{test_values}, $student->{test_values};
      my $OK = ($correct == $student);
      Context()->flags->set(setSqrt => 0);
      #
      Value::Error("Check to see if your answer is simplified.") unless $OK;
      return $OK;
    };

########################################################################

Context()->flags->set(
  reduceConstants=>0,
  reduceConstantFormulas=>0,
  formatStudentAnswer=>'reduced'
);

$a = random(3,8,1);
$b = random(2,8,2);

$c = Compute("3*$a^2")->reduce;

$b2 = $b/2;
$ans1 = Compute("$a*y^$b2")->reduce;
$ans = Formula("$ans1 * sqrt(3)");

########################################################################

Context()->texStrings;
BEGIN_TEXT
Simplify the expression
\[
\sqrt{$c y^{$b}}
\]
into the simplest radical form.  Assume \(y\geq 0\).
$PAR
Answer: \{ans_rule(20)\}
END_TEXT
Context()->normalStrings;

########################################################################

ANS( $ans->cmp(checker=>$reducedSqrt) );

ENDDOCUMENT();        
